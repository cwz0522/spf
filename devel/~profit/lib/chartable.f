\ REQUIRE стек ~profit/lib/stacks.f
REQUIRE /TEST ~profit/lib/testing.f
REQUIRE ON lib/ext/onoff.f
REQUIRE FOR ~profit/lib/for-next.f
REQUIRE буффер ~profit/lib/collectors.f

\ Автомат, принимающий поток символов. Слово "состояние" создаёт
\ одно состояние этого автомата с 256-ю выходами, где каждый выход
\ -- реакция на ввод в автомат символа. Действие может включать
\ в себя и переключение автомата в другое состояние. При входе
\ автомата в состояние может быть выполнено какое-нибудь действие ("на-входе:")
\ Чтобы запустить автомат, нужно установить-курсор, запустить какое-нибудь уже
\ созданное состояние, и только потом запускать словом "-символов-обработать" с
\ кол-вом символов на стеке (см. слово пустить-автомат в примере).
\ Идеально для реализации сканеров языков программирования.

\ Кроме того, определяется также слово "таблица" создающее только одно
\ состояние, но с произвольным кол-м выходов. Его удобно использовать
\ как замену CASE.

MODULE: chartable
0 VALUE текущее-состояние
0 VALUE граница-обработки

VARIABLE /символ
EXPORT

: символ ( -- c ) /символ @ ; \ выдаёт текущий символ

' NOOP CONSTANT отдыхают

DEFINITIONS

: адрес-символа ( n -- addr ) CELLS текущее-состояние + ; \ n -- номер символа, addr -- соотв. ему ячейка в состоянии
: -й-символ ( xt c -- ) адрес-символа ! ;
: установить-диапазон ( xt start end  -- ) 1+ SWAP DO DUP I -й-символ LOOP DROP ;
: все-символы ( xt -- ) 0 255 установить-диапазон ;
: очистить-все-символы ( -- )  отдыхают все-символы ;

0 VALUE последняя-реакция
0 VALUE предпоследняя-реакция
: :n ( "name" -- xt ) последняя-реакция TO предпоследняя-реакция  :NONAME  DUP TO последняя-реакция ;

EXPORT

: символ:  ( "z" -- ) :n CHAR -й-символ ;
: выполняет: ( n -- ) :n SWAP -й-символ ;
: asc: ( n -- ) выполняет: ;

: все: ( -- ) :n   все-символы ;
: диапазон: ( a b -- ) :n -ROT установить-диапазон ;

: пробел: ( -- ) :n BL -й-символ ;
: перевод-строки: ( -- ) отдыхают 13 -й-символ  :n 10 -й-символ ;
: разделители: ( -- ) :n 0 32 установить-диапазон ;
: цифры: ( -- ) [CHAR] 0 [CHAR] 9 диапазон: ;

: латинские-буквы: ( -- ) [CHAR] a [CHAR] z диапазон:
последняя-реакция [CHAR] A [CHAR] Z установить-диапазон ;

: all-asc: ( addr u -- ) :n -ROT OVER + SWAP DO DUP I C@ -й-символ LOOP DROP 	;

: символы: ( "ABCZ" -- ) ParseWord all-asc: ;

: тоже-самое ( -- ) предпоследняя-реакция COMPILE, ; IMMEDIATE
: меня ( -- ) LATEST COUNT SLIT, ; IMMEDIATE

0 VALUE сигнал

: таблица ( число-случаев "имя" -- )
CREATE
DUP 1+ , \ кол-во случаев плюс один
HERE TO текущее-состояние
0 DO отдыхают , LOOP
отдыхают , \ действие по-умолчанию, для случаев номера которых превышают кол-во состояний
DOES> DUP @ ROT MIN DUP TO сигнал 1+ CELLS + @ EXECUTE ;

: состояние ( -- )
CREATE
отдыхают , \ действие на входе
отдыхают , \ реакция на окончание текстового потока
HERE TO текущее-состояние
255 FOR отдыхают , NEXT
отдыхают , \ действие по-умолчанию, для случаев номера которых превышают кол-во состояний
DOES> DUP @ EXECUTE  2 CELLS + TO текущее-состояние ;


: на-входе: ( -- ) :n -2 -й-символ ;
: строка-кончилась: ( -- ) :n -1 -й-символ ;

VECT обработчик-каждого-символа

: код-для-символа ( -- xt ) символ 255 1 + MIN адрес-символа @ ;
: выполнить-символ   обработчик-каждого-символа  код-для-символа >R ; \ EXECUTE

: выполнить-один-раз ( c -- ) /символ ! выполнить-символ ;
: закончить-обработку -1  адрес-символа @ EXECUTE ;

: взять-из-таблицы ( "tbl ) ' >BODY текущее-состояние 2 CELLS - 255 1 + 2 + CELLS MOVE ;

5 таблица взять-букву ( -- c )
все: ABORT" Неверный размер символа!" ;
1 выполняет: C@ ;
2 выполняет: W@ ;
3 выполняет: @ 0xFFFFFF AND ;
4 выполняет: @ ;

VARIABLE курсор-в-тексте
: отсюда ( -- addr ) курсор-в-тексте @ ;
: пропустить-букву ( -- ) размер-символа курсор-в-тексте +! ;
: дать-букву ( -- c ) отсюда размер-символа взять-букву  пропустить-букву ;
: вернуть-букву ( -- ) размер-символа NEGATE курсор-в-тексте +! ;
: поставить-курсор ( адрес -- ) курсор-в-тексте ! ;

:NONAME CR символ EMIT ." |" отсюда . ; CONSTANT отладка-автомата
: включить-отладку-автомата  отладка-автомата TO обработчик-каждого-символа ;
: отключить-отладку-автомата  NOOP TO обработчик-каждого-символа ;

VARIABLE продолжать-обработку
: обрабатывать-до-сигнала ( -- ) продолжать-обработку ON  BEGIN продолжать-обработку @ WHILE дать-букву выполнить-один-раз REPEAT закончить-обработку ;

( 
100 CELLS стек состояний \ а нужен ли он?
: сохранить-состояние  отсюда граница-обработки состояний 2положить  общий-накопитель 2@ состояний 2положить  текущее-состояние размер-символа состояний 2положить ;
: вернуть-состояние  состояний пусто? IF EXIT THEN  состояний 2взять TO размер-символа TO текущее-состояние  состояний 2взять общий-накопитель 2!  состояний 2взять  TO граница-обработки поставить-курсор ;
)

: остановить-автомат  ( -- ) отсюда 1- TO граница-обработки ;
: запустить ( end -- )
TO граница-обработки          BEGIN
отсюда граница-обработки U<   WHILE
дать-букву выполнить-один-раз REPEAT
закончить-обработку ;

: -символов-обработать ( n -- ) ?DUP IF отсюда + запустить THEN ;
\ сколько нужно символов обработать начиная от текущего 

\ синонимы на латинице
: state состояние ;
: symbol: символ: ;
: all: все: ;
: symbol символ ;
: rollback1 вернуть-букву ;
: on-enter: на-входе: ;
: current-state chartable::текущее-состояние ;
: current-state! chartable::TO текущее-состояние ;
: execute-one выполнить-один-раз ;
: state-table таблица ;
: range: диапазон: ;
: end-input: строка-кончилась: ;
: input-position отсюда ;
: signal сигнал ;

;MODULE

/TEST
9 таблица 1-2-3 \ три состояния 
1 выполняет: ." I" ; 
2 выполняет: ." II" ; 
3 выполняет: ." III" ; 
4 9 диапазон: ." IV-IX" ; 

$> 5 1-2-3
\ вывод: IV-IX

256 таблица c \ таблица обработчиков символов 
все: ." unknown" ; 
разделители: ." delimiter, but not space" ; 
пробел: ." space" ; 
перевод-строки: ." carriage return" ; 
CHAR 0 CHAR 9 диапазон: ." digit" ; 

CHAR a CHAR z диапазон: ." letter" ; 
CHAR A CHAR Z диапазон: тоже-самое ; 
CHAR а CHAR я диапазон: тоже-самое ; 
CHAR А CHAR Я диапазон: тоже-самое ; 

$> .( press a key) KEY CR c

VARIABLE счётчик 

состояние не-считать 
состояние прибавлять 
состояние отнимать 

не-считать
на-входе: CR ." |" ;
\ все: ;  \ необязательно, по-умолчанию так и есть 
символ: | не-считать ;
символ: +  прибавлять ;
символ: -  отнимать ; 

прибавлять
взять-из-таблицы не-считать \ копируем обработчики |+-
на-входе: CR ." +" ;
CHAR 0 CHAR 9 диапазон:  счётчик 1+! ; 

отнимать
взять-из-таблицы не-считать \ копируем обработчики |+-
на-входе: CR ." -" ;
CHAR 0 CHAR 9 диапазон:  -1 счётчик +!  ; 

: пустить-автомат ( a u — ) 
счётчик 0!
1 TO размер-символа SWAP поставить-курсор 
не-считать 
-символов-обработать ; 
включить-отладку-автомата
$> S" 12+345-67|90" пустить-автомат  CR счётчик @ .
\ вывод: 1