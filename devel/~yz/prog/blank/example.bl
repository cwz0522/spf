\ BLANK - это обыкновенная Форт-система на основе SP-Forth 3.7
\ Андрея Черезова со всеми вытекающими отсюда последствиями.
\ В частности, она чувствительна к регистру букв :-(
\ Комментарии начинаются с обратной дробной черты
( или пишутся в круглых скобках)

\ до начала самой формы можно описать какие-нибудь служебные переменные 
\ или определить шрифты
" Times New Roman Cyr" 20 Шрифт таймс
" Times New Roman Cyr" 20 жирный Шрифт таймс-жирный

\ начало формы. Указываются ее размеры и название
700 800 " Пример" Форма

\ в первом ряду будет два элемента
2 ===

\ первый из них - просто надпись шрифтом Таймс
таймс
" Просто надпись" Надпись

\ второй - надпись жирным шрифтом
таймс-жирный
" надпись жирным шрифтом" Надпись
Обычный \ вернемся к обыкновенному шрифту - он ничуть не хуже

\ начинаем второй ряд
3 ===

\ начинаем с картинки
10 %   \ места она займет немного - всего десять процентов ширины окна
(| " windir" GETENV " %s\\Треугольники.bmp" |) Рисунок

\ а теперь тоже картинка, только из файла другого типа
\ " blank.ico" Иконка

\ отчеркнем все это линией
1 ===      
Линия 

3 ===     

\ Элементы в клетке можно выравнивать 
<< " влево" Надпись
\ все элементы выравниваются влево по умолчанию, 
\ так что << можно и не ставить

|| " по центру" Надпись
>> " вправо" Надпись

1 ===
Линия

\ стандартные элементы управления
2 ===

" check box" Квадратик кв1  \ не знаю, как он официально называется
\ Такой квадрат, в котором мышкой отмечают

\ Ну это понятно. Выпадающий список.
Список список1
А это|значения|из списка|Они разделяются|символом|вертикальной черты
Список|может занимать|несколько строк
Конец-списка

\ пропустим для красоты одну строку
0 ===

4 ===

10 % " строка: " Надпись
10 Ввод строка1     \ строка ввода на 10 символов

>> " число: " Надпись
3 Ввод-числа число1  \ строка для ввода трехзначного числа

0 ===

\ а теперь - то, что по английски почему-то именуется radio buttons
\ сразу встают перед глазами эфирные волны, антенны и Попов с бородой :-)
3 ===
Группа кнопки1  \ Это слово еще ничего не рисует
\ Оно только объявляет следующие кнопки взаимозависимыми
" Значение1" Кружок
" Значение2" Кружок
" Значение3" Кружок
\ а вот это уже сами кнопки

0 ===

\ а теперь настоящие кнопки, не какие-нибудь радио
3 ===
\ это обычная кнопка с надписью
|| "    OK   " Кнопка ок
\ Это кнопка с иконкой. Еще бывают кнопки с картинками .bmp
|| " blank.ico" Кнопка ок1

|| " Закрыть" Кнопка закрыть 

\ Описание формы на этом завершено. Теперь начинается раздел обработчиков
\ а попросту говоря - обыкновенных слов, написанных на чистом Форте
\ Слова эти, вызываемые по нажатию описанных выше кнопок, могут делать все, 
\ что угодно - проверять введенные значения, записывать их в файл и т.д.

Обработчики

: show-values
  (| 
   кв1 строка1 число1 список1 кнопки1
   " Квадрат=%d Строка=\q%s\q Число=%d Список=%d Кнопки=%d"
  |) Окошко
;

\ значения будут показываться при нажатии на одну из кнопок
' show-values дляКнопки ок
' show-values дляКнопки ок1

\ а эта кнопка попросту закроет окно
' закрыть-окно дляКнопки закрыть

\ ну и наконец запустим саму форму
Конец-формы

\ а здесь уже писать бесполезно. Управление сюда никогда не доходит.
