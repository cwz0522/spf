( Консольный ввод-вывод.
  ОС-независимые слова [относительно...].
  Copyright [C] 1992-1999 A.Cherezov ac@forth.org
  Преобразование из 16-разрядного в 32-разрядный код - 1995-96гг
  Ревизия - сентябрь 1999
)

32 VALUE BL ( -- char ) \ 94
\ char - значение символа "пробел".


: SPACE ( -- ) \ 94
\ Вывести на экран один пробел.
  BL EMIT
;

: SPACES ( n -- ) \ 94
\ Если n>0 - вывести на дисплей n пробелов.
  BEGIN
    DUP
  WHILE
    BL EMIT 1-
  REPEAT DROP
;

VARIABLE PENDING-CHAR \ клавиатура одна -> переменная глобальная, не USER

: KEY? ( -- flag ) \ 94 FACILITY
\ Если символ доступен, вернуть "истину". Иначе "ложь". Если несимвольное
\ клавиатурное событие доступно, оно отбрасывается и больше недоступно.
\ Символ будет возвращен следующим выполнением KEY.
\ После того как KEY? возвратило значение "истина", следующие выполнения
\ KEY? до выполнения KEY или EKEY также возвращают "истину" без отбрасывания
\ клавиатурных событий.
  PENDING-CHAR @ 0 > IF TRUE EXIT THEN
  BEGIN
    EKEY?
  WHILE
    EKEY  EKEY>CHAR
    IF PENDING-CHAR !
       TRUE EXIT
    THEN
    DROP
  REPEAT FALSE
;

: KEY ( -- char ) \ 94
\ Принять один символ char. Клавиатурные события, не соответствующие
\ символам, отбрасываются и более не доступны.
\ Могут быть приняты все стандартные символы. Символы, принимаемые по KEY,
\ не отображаются на дисплее.
\ Программы, требующие возможность получения управляющих символов,
\ зависят от окружения.
  PENDING-CHAR @ 0 >
  IF PENDING-CHAR @ -1 PENDING-CHAR ! EXIT THEN
  BEGIN
    EKEY  EKEY>CHAR 0=
  WHILE
    DROP
  REPEAT
;
